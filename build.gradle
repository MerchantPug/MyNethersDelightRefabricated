plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.6.2"
}

version = "${mc_version}-${mod_version}.refabricated"
group = mod_group

base {
    archivesName = "MyNethersDelight"
}

loom {
    accessWidenerPath = file("src/main/resources/mynethersdelight.accesswidener")

    mixin {
        defaultRefmapName = "mynethersdelight.refmap.json"
    }
}

sourceSets.main.resources {
    srcDirs += 'src/generated/resources'
}

repositories {

    mavenLocal()

    maven {
        // Farmer's Delight Refabricated
        name = "Greenhouse Maven"
        url = 'https://repo.greenhouse.house/releases/'
    }

    maven { url "https://cursemaven.com" }

    maven { url "https://maven.parchmentmc.org" }

    maven {
        url = "https://api.modrinth.com/maven"
    }

    maven {
        // CraftTweaker, JEI
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }
    maven { url "https://maven.ryanliptak.com/" } // AppleSkin


    maven { url = "https://maven.parchmentmc.org" } // Parchment
    maven { url = "https://maven.fabricmc.net/" } // FAPI, Loader
    maven { url = "https://mvn.devos.one/snapshots/" } // Registrate, Porting Lib, Forge Tags, Milk Lib
    maven { url = "https://mvn.devos.one/releases/" }
    maven { // for Porting Lib: Fabric ASM
        url = "https://jitpack.io/"
    }
    maven { url = "https://maven.shedaniel.me/" } // REI and deps
    maven { url = "https://maven.terraformersmc.com/" } // Mod Menu, EMI
    maven { // Reach Entity Attributes
        url = "https://maven.jamieswhiteshirt.com/libs-release"
        content { includeGroup("com.jamieswhiteshirt") }
    }
    mavenCentral() // Mixin Extras
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"

    mappings loom.layered() {
        it.officialMojangMappings {
            setNameSyntheticMembers(false)
        }
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    modImplementation("com.github.Chocohead:Fabric-ASM:${fabricasm_version}") {
        exclude (group: "net.fabricmc.fabric-api")
    }

    modImplementation("vectorwing:FarmersDelight:${farmersdelight_version}+refabricated") {
        exclude(group: "net.fabricmc")
    }
    modImplementation "curse.maven:farmers-respite-fabric-811003:5252387"
    modImplementation "curse.maven:forge-config-api-port-547434:4583000"
    modImplementation "curse.maven:amendments-896746:5462736"

    // Every compat
    modCompileOnly "curse.maven:selene-499980:5666908"
    modRuntimeOnly "curse.maven:selene-499980:5666908"
    modRuntimeOnly "curse.maven:every-compat-628539:5670282"

    modLocalRuntime("com.terraformersmc:modmenu:${project.modmenu_version}")

    modCompileOnly("com.blamejared.crafttweaker:CraftTweaker-fabric-1.20.1:${crafttweaker_version}")
    // Uncomment the below to test CraftTweaker.
    // modLocalRuntime("com.blamejared.crafttweaker:CraftTweaker-fabric-1.20.1:${crafttweaker_version}")

    modCompileOnly("mezz.jei:jei-${mc_version}-common-api:${jei_version}")
    modCompileOnly("mezz.jei:jei-${mc_version}-fabric-api:${jei_version}")
    // Uncomment the below to test JEI.
    // modLocalRuntime("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}"
    // Uncomment the below to test REI.
    // modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"

    modCompileOnly("dev.emi:emi-fabric:${emi_version}:api") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    // Uncomment the below to test EMI.
    /*
    modLocalRuntime("dev.emi:emi-fabric:${emi_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    // This is also required for EMI as it uses an out of date FAPI.
    modLocalRuntime "net.fabricmc.fabric-api:fabric-api-deprecated:${fabric_api_version}"
     */
}

processResources {
    var replaceProperties = [
            mod_id               : mod_id,
            mod_version          : version
    ]
    // setting them as input
    inputs.properties replaceProperties

    filesMatching("fabric.mod.json") {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

publishMods {
    file = remapJar.archiveFile
    modLoaders.add("fabric")
    changelog = file("CHANGELOG.md").getText()
    version = "${mc_version}-${mod_version}"
    displayName = "${mod_version}"
    type = STABLE

    curseforge {
        projectId = "1093355"
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")

        minecraftVersions.add("1.20")
        minecraftVersions.add("1.20.1")
        javaVersions.add(JavaVersion.VERSION_17)

        clientRequired = true
        serverRequired = true
    }

    modrinth {
        projectId = "uIOfYdnw"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")

        minecraftVersions.add("1.20")
        minecraftVersions.add("1.20.1")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}